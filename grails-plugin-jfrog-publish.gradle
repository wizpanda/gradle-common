// region Release configuration
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.21.0"
    }
}

apply plugin: 'maven-publish'

// Have to reference the implementation-class as application by plugin id doesn't work in external build script 
// check following issue: https://github.com/gradle/gradle/issues/1262
apply plugin: org.jfrog.gradle.plugin.artifactory.ArtifactoryPlugin

jar {
    exclude "com/wizpanda/UrlMappings**"
}

publishing {
    publications {
        wpJFrogMaven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "${pomName}".toString()
                description = "${pomDescription}".toString()
                url = "${pomURL}".toString()
                developers {
                    developer {
                        id = 'sagrawal31'
                        name = 'Shashank Agrawal'
                        email = 'creative@wizpanda.com'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/${pomGitHubSlug}.git".toString()
                    developerConnection = "scm:git:ssh://github.com:wizpanda/kernel.git".toString()
                    url = "https://github.com/${pomGitHubSlug}/tree/main".toString()
                }
            }
        }
    }
}

artifactory {
    contextUrl = "https://wizpanda.jfrog.io/artifactory"
    publish {
        repository {
            repoKey = "default-maven-local"
            username = "${wpArtifactoryUsername}".toString()
            password = "${wpArtifactoryPassword}".toString()
        }
        defaults {
            publications('wpJFrogMaven')
            publishArtifacts = true
        }
    }
}

// endregion
